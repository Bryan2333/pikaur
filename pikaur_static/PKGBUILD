# Maintainer: Yauheni Kirylau <actionless DOT loveless PLUS aur AT gmail MF com>
# shellcheck disable=SC2034,SC2154

_pkgname=pikaur-static
pkgname="${_pkgname}-git"
pkgver=1.26.1
pkgrel=1
pkgdesc='AUR helper without dependencies which asks all questions before installing/building. Static build for recovery situations, similar to `pacman-static`'
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/actionless/pikaur"
license=('GPL-3.0-only')
source=(
	"$pkgname::git+${url}.git#branch=master"
)
md5sums=(
	"SKIP"
)
depends=(
	'git'
)
makedepends=(
	'nuitka'
	'ccache'
	'fish'
	'python-markdown-it-py'
	'python-pysocks'
	'python-defusedxml'
)
optdepends=(
	'devtools: for Arch Pkgs support in -G/--getpkgbuild operation'
	'pacman-contrib: to use in pacman hook/systemd timer for cleaning up pikaur cache'
)
conflicts=("$_pkgname")
provides=("$_pkgname")

pkgver() {
	cd "${srcdir}/${pkgname}" || exit 2
	set -o pipefail
	git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g' || echo 0.0.1
}

build() {
	cd "${srcdir}/${pkgname}"
	sed -i -e "s/VERSION.*=.*/VERSION = '${pkgver}'/g" pikaur/config.py
	make  # @TODO: add `make standalone` target
	cd pikaur_static
	# nuitka compilation fails with default arch flags, so let it decide:
	unset CFLAGS
	unset CXXFLAGS
	unset LDFLAGS
	unset LTOFLAGS
	./make.fish standalone
}

package() {
	cd "${srcdir}/${pkgname}"
	#cp -r ${srcdir}/${pkgname}/packaging/* ${pkgdir}
	install -Dm644 "${srcdir}/${pkgname}/packaging/usr/lib/systemd/user/pikaur-cache.service" \
					"${pkgdir}/usr/lib/systemd/user/${_pkgname}-cache.service"
	install -Dm644 "${srcdir}/${pkgname}/packaging/usr/lib/systemd/user/pikaur-cache.timer" \
					"${pkgdir}/usr/lib/systemd/user/${_pkgname}-cache.timer"
	for langmo in $(cd ./locale && ls ./*.mo); do
		lang=$(sed -e 's/.mo$//' <<< "${langmo}")
		install -Dm644 "locale/${langmo}" "$pkgdir/usr/share/locale/${lang}/LC_MESSAGES/${_pkgname}.mo"
	done
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 pikaur.1 "$pkgdir/usr/share/man/man1/${_pkgname}.1"
	mkdir -p "${pkgdir}/usr/bin/"
	install -D -m755 "${srcdir}/${pkgname}/pikaur_static/pikaur-standalone" "${pkgdir}/usr/bin/${_pkgname}"
}
